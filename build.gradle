plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
}

group 'bot.inker.nanolimbo'
version '1.5'

compileJava {
    options.encoding = "UTF-8"
}

tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'

    implementation 'org.spongepowered:configurate-yaml:4.1.2'
    implementation 'io.netty:netty-all:4.1.85.Final'
    implementation 'net.kyori:adventure-nbt:4.12.0'
    implementation 'com.grack:nanojson:1.7'
}

shadowJar {
    from 'LICENSE'

    manifest {
        attributes('Main-Class': 'ua.nanit.limbo.NanoLimbo')
    }

    minimize()
}

test {
    useJUnitPlatform()
}

// inkerbot start
java{
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            url = 'https://repo.inker.bot/repository/maven-releases/'
            credentials {
                username System.getenv('NEXUS_USERNAME')
                password System.getenv('NEXUS_PASSWORD')
            }
        }
    }
    publications {
        maven(MavenPublication) {
            pom {
                name = 'NanoLimbo'
                description = 'A lightweight minecraft limbo server'
                url = 'https://github.com/Nan1t/NanoLimbo'
                properties = [
                    publishBy: "Inkerbot"
                ]
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'nan1t'
                        name = 'Nan1t'
                        email = 'imnanit@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Nan1t/NanoLimbo.git'
                    developerConnection = 'scm:git:ssh://git@github.com:Nan1t/NanoLimbo.git'
                    url = 'https://github.com/Nan1t/NanoLimbo'
                }
            }
            from components.java
        }
    }
}
// inkerbot end